// FindZeroLogon-DCSync
// Authors: 
//@DebugPrivilege
//@GossiTheDog

let timeframe = 7d;
SecurityEvent
| where TimeGenerated >= ago(timeframe)
| where EventID == 4662 and AccountType=="Machine"
| where Account endswith "$" 
| where Properties has "{1131f6ad-9c07-11d1-f79f-00c04fc2dcd2}"
    or Properties has "{19195a5b-6da0-11d0-afd3-00c04fd930c9}"
| project TimeGenerated, Account, Activity, Properties, SubjectLogonId, Computer
| join (
    SecurityEvent
    | where TimeGenerated >= ago(timeframe)
    | where EventID == 4624
    | where LogonType == 3
    | project EventID, LogonType, Activity, TargetLogonId, IpAddress
    | project-rename SubjectLogonId = TargetLogonId
    ) on SubjectLogonId
    | project TimeGenerated, Account, Computer, IpAddress, LogonType, Activity, Properties
    | sort by TimeGenerated desc
